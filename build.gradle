buildscript {
	ext {
		kotlinVersion = '1.1.2-4'
	        dokka_version = '0.9.13'
	}
	
	repositories {
		mavenCentral()
        	jcenter()
    	}

    	dependencies {
        	classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlinVersion
        	classpath group: 'org.jetbrains.kotlin', name: 'kotlin-allopen', version: kotlinVersion
        	classpath group: 'org.jetbrains.kotlin', name: 'kotlin-noarg', version: kotlinVersion
        	classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"

        	classpath 'pl.allegro.tech.build:axion-release-plugin:1.5.0'
        	classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        	classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    	}
}

apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.bmuschko.nexus'

scmVersion {
    tag {
        prefix = 'chrome-reactive-kotlin'
    }
}

group = 'pl.wendigo'
version = scmVersion.version

jar {
    baseName = 'chrome-reactive-kotlin'
    version = scmVersion.version
}

configurations {
    ktlint
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

compileKotlin {
    kotlinOptions.languageVersion = "1.1"
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.0'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-runtime', version: kotlinVersion
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'

    ktlint group: 'com.github.shyiko', name: 'ktlint', version: '0.6.1' }

modifyPom {
    project {
        name 'Chrome Reactive'
        description 'Chrome Reactive - low level, remote chrome debugger protocol client'
        url 'https://serafin.tech'
        inceptionYear '2017'

        scm {
            connection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
            url 'https://github.com/wendigo/chrome-reactive-kotlin'
            developerConnection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
        }

        scm {
            connection 'scm:git:https://github.com/wendigo/chrome-reactive-kotlin.git'
            url 'https://github.com/wendigo/chrome-reactive-kotlin'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                name 'Mateusz "Serafin" Gajewski'
                email 'contact@serafin.tech'
            }
        }
    }
}

nexusStaging {
    packageGroup = 'pl.wendigo'
    numberOfRetries = 20
    delayBetweenRetriesInMillis = 10000
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

test {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dokka {
    moduleName = 'data'
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    processConfigurations = ['compile', 'extra']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/wendigo/chrome-reactive-kotlin/blob/master/src/main/kotlin"
        suffix = "#L"
    }
    sourceDirs = files('src/main/kotlin')
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}
